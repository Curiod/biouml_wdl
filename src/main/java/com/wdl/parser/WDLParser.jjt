/***************************************************************
                   OPTIONS
***************************************************************/
options
{
  MULTI = true;
  NODE_DEFAULT_VOID = true;
  STATIC = false;
  NODE_PREFIX = "Ast";
  DEBUG_PARSER = false;
  TRACK_TOKENS=true;
}

/***************************************************************
                   PARSER BEGIN
***************************************************************/
PARSER_BEGIN(WDLParser)
package com.wdl.parser;
import java.io.Reader;
import java.io.StringReader;
import java.util.logging.Level;
import java.util.logging.Logger;


public class WDLParser
{
	protected Logger log = Logger.getLogger(WDLParser.class.getName());
	private String version;
	private boolean workflowExist = false;
    /** Default constructor. */
    public WDLParser()
    {
        this (new StringReader("0"));
    }

    public AstStart parse(Reader reader) throws ParseException
    {
        ReInit(reader);
        AstStart astStart = Start();
        return astStart;
    }
    
    public String getVersion()
    {
    	return version;
    }

}


PARSER_END(WDLParser)

/***************************************************************
                       SKIP & TOKENS
***************************************************************/

TOKEN_MGR_DECLS : {
	int prevLexState;
	int braceLevel = 0;
	boolean isHeredoc = false;
}

< DEFAULT > SPECIAL_TOKEN :
{
< EOL :   ("\n" | "\r"  | "\r\n" ) >
}

<*> SPECIAL_TOKEN : /* instead of SKIP */
{
  <([" ", "\t"])+>
| <"#"> { prevLexState = curLexState; }: ML_COMMENT
}

<ML_COMMENT> SPECIAL_TOKEN :
{
  < ML_EOL : < EOL > > {SwitchTo(prevLexState);}
| <~["\n"]>
}

TOKEN : /* RESERVED WORDS */
{
  < TASK : "task" >

| < WORKFLOW : "workflow" >

| < VERSION : "version" >

| < INPUT : "input" >

| < OUTPUT : "output" >

| < PRIMITIVE_TYPE :  "Boolean" | "File" |
                    "Float"| "Int"| "String" >
| < ARRAY : "Array">

| < NONE : "None" >

| < OBJECT_TYPE : "Object" >

| < PAIR : "Pair" >

| < MAP : "Map">

| < ALIAS : "alias" >

| < AS : "as" >

| < CALL : "call" >

| < ELSE : "else" >

| < TRUE_OR_FALSE : ("true"|"false") >

| < IF : "if" >

| < IN : "in" >

| < IMPORT : "import" >

| < LEFT : "left" >

| < META : "meta" >

| < OBJECT: "object" >

| < PARAMETER_META : "parameter_meta" >

| < RIGHT : "right" >

| < RUNTIME: "runtime" >

| < SCATTER : "scatter" >

| < STRUCT : "struct" >

| < THEN : "then" >

| < NULL : "null" >

| < STANDARD_FUNCTION :( /* 1.0 */
						"floor" | "ceil" | "round" | "sub" | "base_name" |
						"glob" | "size" | "stdout" | "stderr" | "read_string" |
						"read_int" | "read_float" | "read_boolean" | 
						"read_lines" | "write_lines" | "read_tsv" | 
						"write_tsv" | "read_map" | "write_map" | "read_json" | 
						"write_json" | "read_object" | "read_objects" | 
						"write_object" | "write_objects" | "prefix" | 
						"length" | "range" | "transpose" | "cross" | "zip" | 
						"flatten" | "select_first" | "select_all" | "defined" |
						/* 1.1 */
						"min" | "max" | "suffix" | "quote" | "squote" | "sep" |
						"unzip" | "as_map" | "as_pairs" | "keys" | 
						"collect_by_key" ) >

| < AND: "&&">

| < OR : "||">

| < NOT : "!" >

| < BRAKET_L : "(">

| < BRAKET_R : ")">

| < COLON : ":">

| < EQUAL : "=" >

| < PLUS : "+">

| < MINUS : "-">

| < SEMICOLON : ";">

| < COMMA : "," >

| < SUBSYMBOL : < SYMBOL > ("." < SYMBOL >)+ >

}

TOKEN : /* SPECIAL SYMBOLS */
{
< QUESTION_MARK: "?" >
}

TOKEN : /* STATE SWITCHERS */
{
< COMMAND: "command" > : BASH_COMMAND
}

< BASH_COMMAND > TOKEN:
{
< COMMAND_HEREDOC_START: "<<<" > {isHeredoc = true;}: BASH_STRING
| < COMMAND_START:  < BRACE_L > > 
{braceLevel+=1;}: BASH_STRING
}

< BASH_STRING > TOKEN:
{
< START : < BRACE_L > > {braceLevel+=1;}
| < COMMAND_END:  < BRACE_R > > 
{
	braceLevel-=1;
	if (braceLevel == 0 && !isHeredoc)
	{
		isHeredoc = false;
		SwitchTo(DEFAULT);
	}
}
| < COMMAND_HEREDOC_END: ">>>" > : DEFAULT
| < ANY : (~["{", "}"]) >
}

TOKEN : /* RESERVED FOR FUTURE VERSIONS */
{
 < DIRECTORY: "Directory" >
 | < HINTS: "hints" >
 | < REQUIREMENTS:  "requirements" >
}

TOKEN : /* LITERALS */
{
  < TEXT_STRING : ("\""  ( ~[ "\"", "\n", "\t" ] )* "\"")
    | ("'" ( ~[ "'", "\n", "\t" ] )* "'")> //TODO: /"~"(?!{)/, /"$"(?!{)/
  | < OCTAL_ESCAPE : "\\" ((["0"-"7"]){1, 3}) >
  | < HEX_ESCAPE : "\\x" ((["0"-"9","a"-"f","A"-"F"]){2}) >
  | < UNICODE : ("\\u" ((["0"-"9","a"-"f","A"-"F"]){4}))
    | ("\\U" ((["0"-"9","a"-"f","A"-"F"]){8}))>
}

TOKEN : /* IDENTIFIERS */
{
  < #LETTER : [ "a"-"z", "A"-"Z" ] >
| < #DIGIT : [ "0"-"9" ] >
| < SYMBOL : ( < LETTER > | "_" ) ( < LETTER > | < DIGIT > | "_" )* >
| < INTEGER: (<DIGIT>)+>
| < TRIANGULAR_BRACKET_L : "<" >
| < TRIANGULAR_BRACKET_R : ">" >
| < BRACE_L: "{" >
| < BRACE_R: "}" >
| < SQUARE_BRACKET_L : "[" >
| < SQUARE_BRACKET_R : "]" >
| < OPERATOR : [ "/", "%"] >
| < STAR : "*" >
| < #EXPONENT: ["e","E"] (< PLUS > | < MINUS >)? (["0"-"9"])+ >
| < FLOATING_POINT_LITERAL:
            (["0"-"9"])+ "." (["0"-"9"])* (<EXPONENT>)?
            | "." (["0"-"9"])+ (<EXPONENT>)?
            | (["0"-"9"])+ (<EXPONENT>)?
    >
}

/***************************************************************
                   GRAMMAR START
***************************************************************/
AstStart Start() #Start:
{}
{
    version()

    (astElement())+
    
    <EOF>
    {
        return jjtThis;
    }
}

void astElement():
{}
{
	( task()| workflow() | struct() | importWDL())
}

// version of WDL
void version() #Version:
{}
{
    < VERSION > < FLOATING_POINT_LITERAL > {this.version = token.image;}
}

void task() #Task:
{}
{
    < TASK >
    symbol()
    ((< BRACE_L > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
    (input() | 
    declaration(true) 
    | output() | command() 
    | runtime()  //deprecated at 1.2 and removed from 2.0
    | requirements() //introduced in 1.2
    | hints() // introduced in 1.2
    | meta() | parameter_meta())*
    ((< BRACE_R > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
}

void meta() #Meta:
{}
{
	< META >
	((< BRACE_L > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
    (
    	symbol() 
        (< COLON > #RegularFormulaElement)
        ( 
        	metadataFormulaElement()
			| metaArray()
			| metaMap()
		)
    )*
    ((< BRACE_R > {jjtThis.setElement(token.image);}) #RegularFormulaElement)

}

void parameter_meta() #Meta:
{}
{
	< PARAMETER_META >
	((< BRACE_L > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
    (
    	symbol() 
        (< COLON > #RegularFormulaElement)
        ( 
        	metadataFormulaElement()
			| metaArray()
			| metaMap()		
		)
    )*
    ((< BRACE_R > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
}

void metadataFormulaElement() #MetaFormulaElement:
{}
{
	(< TRUE_OR_FALSE >
    | < TEXT_STRING >
    | < FLOATING_POINT_LITERAL > 
    | < NULL > )
}

void metaArray() #Array:
{
}
{
	((< SQUARE_BRACKET_L > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
	(
	    metadataFormulaElement()
        (
            (< COMMA > #Comma)
            metadataFormulaElement()
        )*
    )?
	((< SQUARE_BRACKET_R > {jjtThis.setElement(token.image);}) #RegularFormulaElement)

}

void metaMap() #Map:
{
}
{
	((< BRACE_L > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
    metaKeyValue()
    (
        (< COMMA > #Comma)
        metaKeyValue()
    )*
	((< BRACE_R > {jjtThis.setElement(token.image);}) #RegularFormulaElement)

}

void metaKeyValue() #KeyValue:
{}
{
	< SYMBOL > {jjtThis.setKey(token.image);}
	(< COLON > #Colon) 
	metadataFormulaElement()
}

void workflow() #Workflow:
{
	if (workflowExist)
		throw new ParseException("The file can contain only one workflow declaration");
}
{
    < WORKFLOW >
    symbol() {jjtThis.setName(token.image);}
    ((< BRACE_L > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
    (input()
    | declaration(true)
    | output() | call() | scatter()
    | meta() | parameter_meta() | conditional())*
    ((< BRACE_R > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
    {this.workflowExist = true;}
}

void scatter() #Scatter:
{}
{
    < SCATTER > // TODO:
    ((< BRAKET_L > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
    symbol()
    (< IN >  #In)
    expression()
    ((< BRAKET_R > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
    
    ((< BRACE_L > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
    (
    declaration(true)
    | call() | scatter() | conditional())*
    ((< BRACE_R > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
}

void conditional() #Conditional:
{}
{
	< IF >
	((< BRAKET_L > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
	expression()
	((< BRAKET_R > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
	
	((< BRACE_L > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
    (
    declaration(true) 
    | call() 
    | scatter() 
    | conditional())*
    ((< BRACE_R > {jjtThis.setElement(token.image);}) #RegularFormulaElement)

}

void command() #Command:
{}
{
    < COMMAND > 
    (
    (
		((< COMMAND_HEREDOC_START > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
		((< ANY > | < START > | < COMMAND_END > )* #BashString)
		((< COMMAND_HEREDOC_END > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
	)
	|
    (
		((< COMMAND_START > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
		( (bashString())* #BashString)
		((< COMMAND_END > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
	))
}

void bashString():
{}
{
	placeholder() | < ANY > | < COMMAND_HEREDOC_END >
}

void placeholder():
{}
{
	< START > 
	(bashString())*
	< COMMAND_END > 
}

void call() #Call:
{}
{
    < CALL >
    (< SYMBOL > #Symbol)
    (
        as()
    )?
    (
    ((< BRACE_L > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
        (< INPUT > #Input)
        (< COLON > #Colon) 
        equationSymbol()
        ((< COMMA > #Comma) equationSymbol())*
        
    ((< BRACE_R > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
    )?
}


void equationSymbol() #Symbol:
{}
{
	< SYMBOL > { jjtThis.setName(token.image); }
	((< EQUAL > #Equal)
	expression())?
}

void runtime() #Runtime:
{
	switch (this.version) {
		case ("1.2"):
			log.log(Level.WARNING, "The runtime section is deprecated and will be removed in WDL 2.0.");
			break;
		default:
			break;
	}
}
{
    < RUNTIME > 
    ((< BRACE_L > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
        (
        symbol() 
        (< COLON > #RegularFormulaElement)
        expression() 
        )*
    ((< BRACE_R > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
}

void requirements() #Runtime:
{
	switch (this.version) {
		case ("1.0"):
		case ("1.1"):
			throw new ParseException("The requirements section was introduced in WDL 1.2. Use \"runtime\" instead.");
		default:
			break;
	}
}
{
    < REQUIREMENTS >
    ((< BRACE_L > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
        (
        symbol() 
        (< COLON > #RegularFormulaElement)
        expression() 
        )*
    ((< BRACE_R > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
}

void hints() #Hints:
{
	switch (this.version) {
		case ("1.0"):
		case ("1.1"):
			throw new ParseException("The hints section was introduced in WDL 1.2. Specify attributes in \"runtime\" instead.");
		default:
			break;
	}
}
{
    < HINTS >
    ((< BRACE_L > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
        (
        symbol() 
        (< COLON > #RegularFormulaElement)
        expression() 
        )*
    ((< BRACE_R > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
}

void input() #Input:
{}
{
    < INPUT >
    ((< BRACE_L > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
    (declaration(true))*
    ((< BRACE_R > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
}

void output() #Output:
{}
{
    < OUTPUT >
    ((< BRACE_L > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
    (declaration(true))*
    ((< BRACE_R > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
}

void struct() #Struct:
{}
{
    < STRUCT >
    symbol() {jjtThis.setStructName(token.image);}
    ((< BRACE_L > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
    (declaration(false))*
    ((< BRACE_R > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
}

void declaration(boolean canBeInitialized) #Declaration:
{
}
{
    type() {jjtThis.setType(token.image.replace(" ", ""));}
    (
        < QUESTION_MARK > // parameter can be undefined
    )?

    (< SYMBOL > { jjtThis.setName(token.image);}	#Symbol)
    
    
    
    (
    ((< EQUAL > 
    {
    	if (!canBeInitialized)
    		throw new ParseException("Variable can not be initialized at line " + token.beginLine);
    	jjtThis.setElement(token.image);
    }
    ) #RegularFormulaElement) 
    expression()
    )?
    
}

void type():
{
}
{
    ((< PRIMITIVE_TYPE > | < OBJECT_TYPE > | < SYMBOL >)
    #Type)
    | arrayType()
    | mapType() 
    | pairType() 
}

void arrayType() #ArrayType:
{}
{
    < ARRAY >
    ((< SQUARE_BRACKET_L > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
    type()
    ((< SQUARE_BRACKET_R > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
    ((( < PLUS > {jjtThis.setElement(token.image);}) #RegularFormulaElement) {jjtThis.setNonEmpty(true);})? 
}

void pairType() #PairType: // TODO: values accessed via left and right
{}
{
    < PAIR >
    ((< SQUARE_BRACKET_L > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
    type()
    (< COMMA > #Comma)
    type()
    ((< SQUARE_BRACKET_R > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
}

void mapType() #MapType:
{}
{
    < MAP >
    ((< SQUARE_BRACKET_L > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
    type()
    (< COMMA > #Comma)
    type()
    ((< SQUARE_BRACKET_R > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
}

void ternary() #Ternary:
{}
{
    < IF >
    expression()
    (< THEN > #Then)
    expression()
    (< ELSE > #Else)
    expression()
}

void expression() #Expression:
{}
{
    (LOOKAHEAD(2) constructor()
    | LOOKAHEAD(10) expressionInBrackets() 
    | text()
    | array()
    | map()
    | pair()
    | LOOKAHEAD(2) containerElement()
    | LOOKAHEAD(2)function()
    | (< NONE > #None)
    | regularFormulaElement()
    | ternary())+
}

void expressionInBrackets() #Expression:
{}
{
    (< BRAKET_L > {jjtThis.setElement(token.image);} #RegularFormulaElement)
    expression()
    (< BRAKET_R > {jjtThis.setElement(token.image);} #RegularFormulaElement)
}

void containerElement() #ContainerElement:
{}
{
    < SYMBOL > {jjtThis.setContainerName(token.image);}
    < SQUARE_BRACKET_L >
    ( < TEXT_STRING> | < SYMBOL > | < SUBSYMBOL> | < INTEGER > ) {jjtThis.setKey(token.image);}
    < SQUARE_BRACKET_R >
}

void constructor() #Constructor:
{}
{
    (< OBJECT > | < SYMBOL >) {jjtThis.setName(token.image);}
    ((< BRACE_L > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
    (
        constructorKeyValue()
        (
	        (< COMMA > #Comma)
	        constructorKeyValue()
        )*
    )?
    ((< BRACE_R > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
}

void constructorKeyValue() #KeyValue:
{}
{
	< SYMBOL > {jjtThis.setKey(token.image);}
	(< COLON > #Colon) 
	expression()
}

void importWDL() #Import:
{
}
{
    < IMPORT >
    text()
    as()
    (alias())*
}


void alias() #Alias:
{}
{
	< ALIAS >
	symbol()
	as()
}


void text() #Text:
{
}
{
    < TEXT_STRING >
    {jjtThis.setText(token.image.substring(1, token.image.length() - 1));}
}

void function() #Function:
{}
{
    (< SYMBOL > | < STANDARD_FUNCTION >){jjtThis.setName(token.image); }
    (< BRAKET_L > {jjtThis.setElement(token.image);} #RegularFormulaElement)
    (
        expression()
        (
            < COMMA > #Comma
            expression()
        )*
    )?
    <  BRAKET_R > {jjtThis.setElement(token.image);} #RegularFormulaElement

}

void regularFormulaElement() #RegularFormulaElement:
{
}
{
    (
            < SYMBOL > {jjtThis.setVariable(true);}
            | < TRUE_OR_FALSE >
            | < SUBSYMBOL>  {jjtThis.setVariable(true);}
            | < STAR >
            | < OPERATOR > |  < TRIANGULAR_BRACKET_L > | < TRIANGULAR_BRACKET_R >
            | < NOT > | < EQUAL >
            | < PLUS > | < MINUS > | <AND> | <OR>
            | < FLOATING_POINT_LITERAL > {jjtThis.setNumber(true);}
            | < INTEGER >

    ){jjtThis.setElement(token.image);}
}




void as() #As:
{}
{
    < AS >
    symbol()
}


void symbol() #Symbol:
{
}
{
  < SYMBOL > { jjtThis.setName(token.image); }
}

void subSymbol()#SubSymbol:
{}
{
    < SUBSYMBOL >
    {
        jjtThis.setName(token.image);
    }
}

void array() #Array:
{
}
{
	((< SQUARE_BRACKET_L > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
	(
	    expression()
        (
            (< COMMA > #Comma)
            expression()
        )*
    )?
	((< SQUARE_BRACKET_R > {jjtThis.setElement(token.image);}) #RegularFormulaElement)

}

void map() #Map:
{
}
{
	((< BRACE_L > {jjtThis.setElement(token.image);}) #RegularFormulaElement)
    keyValue()
    (
        (< COMMA > #Comma)
        keyValue()
    )*
	((< BRACE_R > {jjtThis.setElement(token.image);}) #RegularFormulaElement)

}

void keyValue() #KeyValue:
{}
{
	primitiveType() {jjtThis.setKey(token.image);}
    (< COLON > #Colon)
    expression()
}

void primitiveType():
{}
{
    < SYMBOL > 
    | < TRUE_OR_FALSE >
    | < FLOATING_POINT_LITERAL > 
    | < INTEGER >
    | < SUBSYMBOL>
    | < TEXT_STRING > 
}

void pair() #Pair:
{
}
{
	(< BRAKET_L > {jjtThis.setElement(token.image);}) #RegularFormulaElement
	    expression()
        (< COMMA > #Comma)
        expression()
	(< BRAKET_R > {jjtThis.setElement(token.image);}) #RegularFormulaElement

}

/******************************************************* PARSE FORMULE *************************************************/




